buildscript {
  ext {
    springBootVersion = '1.5.3.RELEASE'
  }
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
   classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.3")
    classpath 'com.bmuschko:gradle-docker-plugin:3.2.0'
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.asciidoctor.convert'
apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin


group = 'com.bancoazteca.clienteunico.clientes'
version = '0.0.1-ci-cd'
sourceCompatibility = 1.8


jar {
  baseName = 'servicio-cliente'
  version = ''
}


repositories {
  mavenCentral()
}


ext {
  springBootAdminVersion = '1.5.7'
  springCloudVersion = 'Edgware.SR2'
}


asciidoctor {
  sourceDir 'src/main/asciidoc'
  attributes  \
       'snippets': file('build/snippets'),
          'toc': 'left'
}


dependencies {
  
  
  compile('org.springframework.boot:spring-boot-starter-web')
  compile('org.springframework.boot:spring-boot-starter-actuator')
  compile 'io.opentracing.contrib:opentracing-spring-web-autoconfigure:0.0.4'
  compile 'org.springframework.cloud:spring-cloud-starter-openfeign'
  compile 'com.uber.jaeger:jaeger-core:0.18.0'
  compile 'io.opentracing.brave:brave-opentracing:0.20.0'
  
}

dependencyManagement {
  imports {
    mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
 
  }
}

task createDockerfile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
  destFile = project.file('build/libs/Dockerfile')
  from 'openjdk:8u151-jre-slim'
  maintainer 'jovani.arzate@elektra.com.mx"'
  copyFile 'servicio-cliente.jar', '/opt/servicio-cliente.jar'
 
  exposePort 8081
  entryPoint 'java', "-Djava.awt.headless=true", "-Xms256m", "-Xmx256m", '-jar', '/opt/servicio-cliente.jar'
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  dependsOn createDockerfile
  inputDir = createDockerfile.destFile.parentFile
  tag = 'jovaniac/servicio-cliente:' + project.version.toLowerCase()
}

task pushImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
  dependsOn buildImage
  imageName = buildImage.tag
}

createDockerfile.dependsOn bootRepackage